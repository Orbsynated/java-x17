import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id "com.github.johnrengelman.shadow"
    id 'java-library'
    id 'java'
    id 'maven-publish'
}

repositories {
    jcenter()
}

sourceSets.main {
    java {
        srcDirs = ['Hash']
    }
}
sourceSets.test {
    java {
        srcDirs = ['test/Hash']
    }
}

dependencies {
    api 'com.google.guava:guava:30.0-android'
    implementation 'com.madgag.spongycastle:core:1.58.0.0'

    testImplementation 'junit:junit:4.13.1'

}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'

jar.manifest.attributes('Implementation-Version': version)

test {
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

task fatJar(type: ShadowJar) {
    destinationDirectory.set(file("$rootDir/lib"))
    archiveFileName.set('java-x17.jar')
    from sourceSets.main.output
    configurations = [project.configurations.runtimeClasspath]
    manifest {
        attributes(
                'Main-Class': 'Hash.X17',
                'Implementation-Version': archiveVersion.get())
    }
}

assemble.dependsOn fatJar
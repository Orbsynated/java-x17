plugins {
    id 'distribution'
    id 'signing'
    id 'idea'
}

allprojects {
    group = 'io.github.orbsynated'
    version = '1.1.0'
}

// Add repositories
allprojects { Project project ->
    afterEvaluate {
        if (pluginManager.hasPlugin('maven-publish')) {
            configure(project) {
                publishing {
                    repositories {
                        maven {
                            name = 'Github'
                            url = uri('https://maven.pkg.github.com/Orbsynated/java-x17')
                            credentials {
                                username = findProperty('JAVA_X17_GITHUB_USERNAME')
                                password = findProperty('JAVA_X17_GITHUB_TOKEN')
                            }
                        }
                        maven {
                            name = 'Sonatype'
                            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                            credentials(PasswordCredentials) {
                                username = findProperty('PROGUARD_STAGING_USERNAME')
                                password = findProperty('PROGUARD_STAGING_PASSWORD')
                            }
                        }
                    }
                }
            }
        }
    }
}

// Add publication
allprojects { Project project ->
    afterEvaluate {
        if (pluginManager.hasPlugin('maven-publish')) {
            configure(project) {
                publishing {
                    publications {
                        create(project.name, MavenPublication) {
                            artifactId = "java-${project.name.toLowerCase()}"
                            versionMapping {
                                usage('java-api') {
                                    fromResolutionOf('runtimeClasspath')
                                }
                                usage('java-runtime') {
                                    fromResolutionResult()
                                }
                            }
                            pom {
                                name = "$group:$artifactId"
                                url = "https://github.com/Orbsynated/java-x17"
                                description = "Library that implements the X17 hash and required functions in Java."
                                licenses {
                                    license {
                                        name = 'GNU General Public License, Version 2'
                                        url = 'https://www.gnu.org/licenses/gpl-2.0.txt'
                                        distribution = 'repo'
                                    }
                                }
                                issueManagement {
                                    system = 'Github Tracker'
                                    url = 'https://github.com/Orbsynated/java-x17/issues'
                                }
                                scm {
                                    url = 'https://github.com/Orbsynated/java-x17.git'
                                    connection = 'scm:git:https://github.com/Orbsynated/java-x17.git'
                                }
                                developers {
                                    developer {
                                        id = 'orb'
                                        name = 'Orbsynated'
                                        roles = ['Project Administrator', 'Developer']
                                    }
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

// Configure default publication (all Java projects)
allprojects { Project project ->
    afterEvaluate {
        if  (pluginManager.hasPlugin('maven-publish') && pluginManager.hasPlugin('java')) {
            configure(project) {
                javadoc {
                    options.addStringOption('Xdoclint:none', '-quiet')
                    if(JavaVersion.current().isJava9Compatible()) {
                        options.addBooleanOption('html5', true)
                    }
                }
                java {
                    withJavadocJar()
                    withSourcesJar()
                }
                publishing {
                    publications {
                        getByName(project.name) {
                            from components.java
                        }
                    }
                }
                if (findProperty('SHOULD_SIGN')) {
                    signing {
                        // signing.gnupg.keyName and signing.gnupg.passphrase is set in .gradle/gradle.properties
                        useGpgCmd()
                        sign publishing.publications.getByName(project.name)
                    }
                }
            }
        }
    }
}


distributions {
    main {
        distributionBaseName.set('Java-X17')
        contents {
            into('lib') {
                from tasks.getByPath(':x17:fatJar').outputs
            }
            from(rootDir) {
                include 'LICENSE'
            }
        }
    }
}

distTar {
    compression = Compression.GZIP
    archiveExtension.set('tar.gz')
}

clean {
    delete file("$rootDir/lib")
}
